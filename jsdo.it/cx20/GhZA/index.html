<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" /><title>新燃岳付近の3Dデータを表示してみた</title>
<meta name="Description" content="jsdo.it - share JavaScript, HTML5 and CSS - " />
<meta name="Keywords"  content="WebGL,JavaScript,HTML5,CSS" />
<meta name="viewport" content="width=device-width, user-scalable=no, initial-scale=1, maximum-scale=1">
<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />

</head>
<body>

<!-- three.js -->
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/108/three.js"></script>
 
<!-- OrbitControls.js -->
<script src="https://rawcdn.githack.com/mrdoob/three.js/r108/examples/js/controls/OrbitControls.js"></script>

<script src="https://cdn.rawgit.com/squarefeet/ShaderParticleEngine/ecc2886c/build/SPE.js"></script>

<!-- dat.GUI.js -->
<script src="../../assets/q/S/i/g/qSigr"></script>

<div id="webgl" style="background-color:#e6e6fa"></div>

<script id="vertexShader" type="x-shader/x-vertex">
uniform sampler2D maskTexture;
uniform sampler2D noiseTexture;
uniform float noiseScale;

uniform sampler2D bumpTexture;
uniform float bumpSpeed;
uniform float bumpScale;

uniform float time;
uniform bool lava;

varying vec2 vUv;

void main() 
{ 
    vUv = uv;
    
    vec4 color_mask = texture2D( maskTexture, vUv );
    vec2 uvTimeShift = vUv + vec2( 1.1, 1.9 ) * time * bumpSpeed;
    vec4 noiseGeneratorTimeShift = texture2D( noiseTexture, uvTimeShift );
    vec2 uvNoiseTimeShift = vUv + noiseScale * vec2( noiseGeneratorTimeShift.r, noiseGeneratorTimeShift.g );
    // below, using uvTimeShift seems to result in more of a "rippling" effect
    //   while uvNoiseTimeShift seems to result in more of a "shivering" effect
    vec4 bumpData = texture2D( bumpTexture, uvTimeShift ) * 0.5;

    // move the position along the normal
    //  but displace the vertices at the poles by the same amount
    float displacement = ( vUv.y > 0.999 || vUv.y < 0.001 ) ? bumpScale * (0.02 * sin(time)) :  bumpScale * bumpData.r * 0.1;
    vec3 newPosition = position + normal * displacement;
    
    if ( color_mask.r >= 1.0 && lava ) {
        gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
    } else {
        gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    }
}
</script>


<script id="fragment_shader1" type="x-shader/x-fragment">
uniform sampler2D originalTexture;
uniform sampler2D maskTexture;

uniform sampler2D baseTexture;
uniform float baseSpeed;
uniform float repeatS;
uniform float repeatT;

uniform sampler2D noiseTexture;
uniform float noiseScale;

uniform sampler2D blendTexture;
uniform float blendSpeed;
uniform float blendOffset;

uniform float time;
uniform float alpha;
uniform bool lava;

varying vec2 vUv;

void main() 
{
    vec4 color_original = texture2D( originalTexture, vUv );
    vec4 color_mask = texture2D( maskTexture, vUv );

    vec2 uvTimeShift = vUv + vec2( -0.7, 1.5 ) * time * baseSpeed;    
    vec4 noiseGeneratorTimeShift = texture2D( noiseTexture, uvTimeShift );
    vec2 uvNoiseTimeShift = vUv + noiseScale * vec2( noiseGeneratorTimeShift.r, noiseGeneratorTimeShift.b );
    vec4 baseColor = texture2D( baseTexture, uvNoiseTimeShift * vec2(repeatS, repeatT) );

    vec2 uvTimeShift2 = vUv + vec2( 1.3, -1.7 ) * time * blendSpeed;    
    vec4 noiseGeneratorTimeShift2 = texture2D( noiseTexture, uvTimeShift2 );
    vec2 uvNoiseTimeShift2 = vUv + noiseScale * vec2( noiseGeneratorTimeShift2.g, noiseGeneratorTimeShift2.b );
    vec4 blendColor = texture2D( blendTexture, uvNoiseTimeShift2 * vec2(repeatS, repeatT) ) - blendOffset * vec4(1.0, 1.0, 1.0, 1.0);

    vec4 theColor = baseColor + blendColor;
    theColor.a = alpha;
    //gl_FragColor = theColor;
    if ( color_mask.r >= 1.0 && lava ) {
        gl_FragColor = theColor;
    } else {
        gl_FragColor = color_original;
    }
}  
</script>

<script src="./index.js"></script>

<body>
</html>
